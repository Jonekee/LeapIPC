set(PACKAGE_VERSION "@LeapIPC_VERSION@")
set(PACKAGE_VERSION_MAJOR "@LeapIPC_VERSION_MAJOR@")
set(PACKAGE_VERSION_MINOR "@LeapIPC_VERSION_MINOR@")
set(PACKAGE_VERSION_PATCH "@LeapIPC_VERSION_PATCH@")

if(LeapIPC_DEBUG)
  message(STATUS "Debug mode on")
  message(STATUS "Installed LeapIPC_VERSION: ${PACKAGE_VERSION}")

  message(STATUS "Installed LeapIPC_BUILD_ARCHITECTURES: @LeapIPC_BUILD_ARCHITECTURES@")
  message(STATUS "Configured LeapIPC_BUILD_ARCHITECTURES: ${LeapIPC_BUILD_ARCHITECTURES}")

  message(STATUS "Installed CMAKE_SIZEOF_VOID_P: @CMAKE_SIZEOF_VOID_P@")
  message(STATUS "Configured CMAKE_SIZEOF_VOID_P: ${CMAKE_SIZEOF_VOID_P}")
endif()

# If the customer has an override architecture requirement, use that
if(DEFINED LeapIPC_ARCHITECTURE)
  foreach(i IN LISTS LeapIPC_ARCHITECTURE)
    string(REGEX MATCH "amd64|x86_64|x64" LeapIPC_is_x64 ${i})
    string(REGEX MATCH "i386|x86" LeapIPC_is_x86 ${i})
    string(REGEX MATCH "arm" LeapIPC_is_arm ${i})

    # Classify
    if(LeapIPC_is_x86)
      list(APPEND LeapIPC_ARCHITECTURES x86)
    elseif(LeapIPC_is_x64)
      list(APPEND LeapIPC_ARCHITECTURES x64)
    elseif(LeapIPC_is_arm)
      list(APPEND LeapIPC_ARCHITECTURES arm)
    else()
      message(WARNING "Unrecognized architecture ${i}")
    endif()
  endforeach()
else()
  # Try to infer what the user wants
  if(CMAKE_SYSTEM_PROCESSOR STREQUAL "arm")
    set(LeapIPC_ARCHITECTURES arm)
  elseif(CMAKE_SIZEOF_VOID_P EQUAL "8")
    set(LeapIPC_ARCHITECTURES x64)
  else()
    set(LeapIPC_ARCHITECTURES x86)
  endif()
endif()

if(LeapIPC_DEBUG)
  message(STATUS "LeapIPC_ARCHITECTURE: ${LeapIPC_ARCHITECTURE}")
  message(STATUS "LeapIPC_ARCHITECTURES: ${LeapIPC_ARCHITECTURES}")
endif()

# Architectures we were built with
set(LeapIPC_BUILD_ARCHITECTURES @LeapIPC_BUILD_ARCHITECTURES@)
if(LeapIPC_DEBUG)
  message(STATUS "LeapIPC_BUILD_ARCHITECTURES: ${LeapIPC_BUILD_ARCHITECTURES}")
endif()

# Verify that we match all of the architectures requested by the customer
foreach(i IN LISTS LeapIPC_ARCHITECTURES)
  list(FIND LeapIPC_BUILD_ARCHITECTURES ${i} found)
  if(found EQUAL -1)
    set(PACKAGE_VERSION_COMPATIBLE FALSE)
    set(PACKAGE_VERSION_UNSUITABLE TRUE)
    if(LeapIPC_DEBUG)
      message(STATUS "Requested architecture of ${LeapIPC_ARCHITECTURE} not compatible with @LeapIPC_BUILD_ARCHITECTURES@")
    endif()
    return()
  endif()
endforeach()

# Determine whether the user's request (either implied or explicit) for libstdc++ can
# be met by this verison of LeapIPC
if(CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
  if(LeapIPC_DEBUG)
    message(STATUS "Installed LeapIPC_USE_LIBCXX: @LeapIPC_USE_LIBCXX@")
    message(STATUS "Configured LeapIPC_USE_LIBCXX: ${LeapIPC_USE_LIBCXX}")
  endif()
endif()

if(PACKAGE_VERSION VERSION_LESS PACKAGE_FIND_VERSION)
  if(LeapIPC_DEBUG)
    message(STATUS "Our version is less than the requested one, trivially incompatible")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
elseif(NOT PACKAGE_FIND_VERSION_MINOR EQUAL PACKAGE_VERSION_MINOR)
  if(LeapIPC_DEBUG)
    message(STATUS "Minor version mismatch, requested minor is ${PACKAGE_FIND_VERSION_MINOR}, this version is ${PACKAGE_VERSION_MINOR}")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
elseif(NOT PACKAGE_FIND_VERSION_MAJOR EQUAL PACKAGE_VERSION_MAJOR)
  if(LeapIPC_DEBUG)
    message(STATUS "Major version mismatch, failure.")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
else()
  set(PACKAGE_VERSION_COMPATIBLE TRUE)
  if (PACKAGE_VERSION VERSION_EQUAL PACKAGE_FIND_VERSION)
    set(PACKAGE_VERSION_EXACT TRUE)
  endif()

  if(LeapIPC_DEBUG)
    message(STATUS "Using LeapIPC in ${CMAKE_CURRENT_SOURCE_DIR}")
  endif()
endif()
